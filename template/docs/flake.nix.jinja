{
  description = "{{ project_name }} documentation build using sphinx-builder";

  inputs = {
    sphinx-builder.url =
      "git+https://gitlab.gitlab.k0s.lab.bo-tech.de/bv/images/sphinx-builder.git";
    nixpkgs.follows = "sphinx-builder/nixpkgs";
    flake-utils.follows = "sphinx-builder/flake-utils";
  };

  outputs = { self, nixpkgs, flake-utils, sphinx-builder }:
    flake-utils.lib.eachDefaultSystem (system:
      let
        pkgs = nixpkgs.legacyPackages.${system};
        builder = sphinx-builder.packages.${system};
        src = pkgs.lib.cleanSource ./..;
        projectSlug = "{{ project_name | lower | replace(' ', '-') }}";
        docName = "${projectSlug}-docs";
      in
      {
        packages.html = pkgs.stdenv.mkDerivation {
          name = docName;
          inherit src;
          nativeBuildInputs = [
            builder.full-sphinx-env
          ];

          buildPhase = ''
            cd docs
            make html
          '';

          installPhase = ''
            cd docs
            mkdir -p "$out/share/doc/$name"
            cp -r _build/html "$out/share/doc/$name"

            mkdir -p "$out/nix-support"
            echo "doc manual $out/share/doc/$name/html index.html" \
              >> "$out/nix-support/hydra-build-products"
          '';
        };

        packages.pdf = pkgs.stdenv.mkDerivation {
          name = "${docName}-pdf";
          inherit src;
          nativeBuildInputs = [
            builder.full-sphinx-env
          ];

          buildPhase = ''
            cd docs
            export TEXMFVAR="$TMPDIR/texmf-var"
            export TEXMFCONFIG="$TMPDIR/texmf-config"
            export TEXMFHOME="$TMPDIR/texmf-home"
            make latexpdf
          '';

          installPhase = ''
            mkdir -p "$out/share/doc/$name"
            cp _build/latex/*.pdf "$out/share/doc/$name"/

            mkdir -p "$out/nix-support"
            for f in "$out"/share/doc/$name/*.pdf; do
              echo "doc manual $f" >> "$out/nix-support/hydra-build-products"
            done
          '';
        };

        devShells.default = pkgs.mkShell {
          packages = [
            builder.full-sphinx-env
          ];
        };
      });
}
